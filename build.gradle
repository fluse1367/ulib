defaultTasks 'clean', 'build', 'jar'

allprojects {

    def major = 1, minor = 0, patch = 1, type = "DEVELOPMENT-SNAPSHOT"

    group 'eu.software4you.ulib'
    version "$major.$minor.$patch-$type"

    repositories {
        mavenCentral()

        maven {
            url 'https://jitpack.io'
            name 'JitPack'
        }

        maven {
            url 'https://libraries.minecraft.net/'
            name 'Mojang'
        }

        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            name 'SpigotMC'
        }

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
            name 'Sonatype'
        }
    }

    ext {
        gitCommitHash = { ->
            try {
                def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-parse', '--short', 'HEAD'
                    standardOutput = stdout
                }
                return stdout.toString().trim()
            } catch (Exception ignored) {
                return "unknown"
            }
        }

        deployRepoUrl = 'https://gitlab.com/api/v4/projects/19415500/packages/maven'
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    jar.doFirst {
        manifest {
            attributes 'Implementation-Title': project.name
            attributes 'Implementation-Version': version + "-" + gitCommitHash()
            attributes 'Specification-Version': version
            attributes 'Implementation-Vendor': 'Software4You.eu'
            attributes 'Built-By': System.getProperty("user.name")
            attributes 'Build-Jdk': System.getProperty("java.version")
            attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        }
    }
}