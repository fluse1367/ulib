defaultTasks 'clean', 'build', 'jar'

allprojects {

    def major = 1, minor = 0, patch = 1, type = "DEVELOPMENT-SNAPSHOT"

    group 'eu.software4you.ulib'
    version "$major.$minor.$patch-$type"

    repositories {
        mavenCentral()

        maven {
            name 'PaperMC'
            url 'https://papermc.io/repo/repository/maven-public/'
        }
    }

    ext {
        gitCommitHash = { ->
            try {
                def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-parse', '--short', 'HEAD'
                    standardOutput = stdout
                }
                return stdout.toString().trim()
            } catch (Exception ignored) {
                return "unknown"
            }
        }

        deployRepoUrl = 'https://gitlab.com/api/v4/projects/19415500/packages/maven'

        coordsPaper = 'com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT'
        coordsWaterfall = 'io.github.waterfallmc:waterfall-api:1.16-R0.4-SNAPSHOT'
        coordsXSeries = 'com.github.cryptomorin:XSeries:7.5.5'

        coordsJetbrainsAnnotations = 'org.jetbrains:annotations:20.1.0'
        coordsLombok = 'org.projectlombok:lombok:1.18.6'
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    jar.doFirst {
        manifest {
            attributes 'Implementation-Title': project.name
            attributes 'Implementation-Version': version + "-" + gitCommitHash()
            attributes 'Specification-Version': version
            attributes 'Implementation-Vendor': 'Software4You.eu'
            attributes 'Built-By': System.getProperty("user.name")
            attributes 'Build-Jdk': System.getProperty("java.version")
            attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        }
    }
}