import static java.util.Optional.ofNullable

// plugin support
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:6.3.0"
    }
}

ext {
    gitCommitHash = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } catch (Exception ignored) {
            return "unknown"
        }
    }
}

//noinspection GrUnnecessarySemicolon
allprojects {
    // plugins
    {
        apply plugin: 'java-library'
        apply plugin: "io.freefair.lombok"
    }

    // project meta settings
    {
        def major = 2, minor = 1, patch = 0
        // evaluate variation
        String variation
        switch (ofNullable((String) project.findProperty('variation')).orElse('snapshot').toLowerCase()) {
            case 'release':
                variation = ''
                break
            case 'beta':
                variation = "BETA-${ofNullable((String) project.findProperty('beta')).orElse(('0'))}"
                break
            case 'snapshot':
                // fallthrough to default
            default:
                variation = 'SNAPSHOT'
        }

        group 'eu.software4you.ulib'
        version "$major.$minor.$patch${variation.isEmpty() ? '' : "-$variation"}"
    }

    // lib including
    configurations {
        libOnly {

        }
        lib {
            canBeResolved = true
            extendsFrom libOnly
        }
        implementation {
            extendsFrom lib
        }
    }; // <- for somewhat reason the build fails without this semicolon


    // dependencies
    {
        repositories {
            mavenCentral()

        }

        dependencies {
            compileOnly 'org.jetbrains:annotations:22.0.0'
        }
    }

    java {
        sourceCompatibility = '17'
    }

    // manifest data
    jar.manifest.attributes([
            'Implementation-Title'  : project.name,
            'Implementation-Version': "${version}-${gitCommitHash()}",
            'Specification-Version' : version,
            'Implementation-Vendor' : 'Software4You.eu',
            'Built-By'              : "${System.getProperty('os.name')} ${System.getProperty('os.version')}",
            'Build-Jdk'             : "${System.getProperty('java.vendor')} ${System.getProperty('java.version')}",
            'Created-By'            : "Gradle ${gradle.gradleVersion}",
    ])
}