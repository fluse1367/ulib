def children = ['core-api', 'core-impl']


repositories {
    // spigot repo
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // bungeecord repo
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

    // velocity repo
    maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }

}

dependencies {
    children.each {
        implementation project(":$it")
    }

    //noinspection GroovyAssignabilityCheck
    lazyLib('net.sf.jopt-simple:jopt-simple:6.0-alpha-3', 'jopt-simple')

    // spigot
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'

    // bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.17-R0.1-SNAPSHOT'

    // velocity
    compileOnly 'com.velocitypowered:velocity-api:3.0.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'
}

//noinspection GrUnnecessarySemicolon
jar {
    archiveBaseName.set 'ulib-loader'

    // include super module
    def sMods = [];
    {
        dependsOn ':super-module:jar'
        var f = ((org.gradle.jvm.tasks.Jar) project(':super-module').tasks.jar).archiveFile.get().asFile
        from(f.parentFile) {
            include f.name
            into 'META-INF/jars'
        }
        sMods += f.name
    }


    // include ulib modules
    def mods = [];
    {
        children.each {
            dependsOn ":$it:jar"

            var project = project(":$it")
            var f = ((org.gradle.jvm.tasks.Jar) project.tasks.jar).archiveFile.get().asFile
            from(f.parentFile) {
                include f.name
                into 'META-INF/jars'
            }
            mods += f.name
        }
    }

    // include library coordinates
    def libs = [];
    {
        rootProject.subprojects {
            project.configurations.lib.resolvedConfiguration.resolvedArtifacts.each { f ->
                libs += f.moduleVersion.id.toString()
            }
        }
    }

    manifest.attributes([
            'Main-Class'             : 'eu.software4you.ulib.loader.launch.Main',
            'Premain-Class'          : 'eu.software4you.ulib.loader.agent.AgentMain',
            'Agent-Class'            : 'eu.software4you.ulib.loader.agent.AgentMain',
            'Can-Redefine-Classes'   : true,
            'Can-Retransform-Classes': true,
            'Libraries'              : libs,
            'Module-Files'           : mods,
            'Super-Modules'          : sMods
    ])
}

extraJavaModuleInfo {
    automaticModule('bungeecord-api-1.17-R0.1-SNAPSHOT.jar', 'bungeecord.api')
}