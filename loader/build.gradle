repositories {
    // spigot repo
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // bungeecord repo
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

    // velocity repo
    maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }

}

dependencies {
    //noinspection GroovyAssignabilityCheck
    lazyLib('net.sf.jopt-simple:jopt-simple:6.0-alpha-3', 'jopt-simple')

    // spigot
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'

    // bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.17-R0.1-SNAPSHOT'

    // velocity
    compileOnly 'com.velocitypowered:velocity-api:3.0.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'
}

ext {
    projInclude = { List update, String projName ->
        var project = project(":$projName")
        dependencies {
            implementation project
        }

        jar {
            dependsOn ":$projName:jar"

            var f = ((org.gradle.jvm.tasks.Jar) project.tasks.jar).archiveFile.get().asFile
            from(f.parentFile) {
                include f.name
                into 'META-INF/jars'
            }
            update.add(f.name)
        }
    }

    manifestModules = new HashMap<String, List<String>>()

    inclModule = { String baseName ->
        def list = []
        ["$baseName-api", "$baseName-impl"].each { projInclude(list, it) }
        manifestModules.put(baseName, list)
    }
}

//noinspection GrUnnecessarySemicolon
jar {
    archiveBaseName.set 'ulib-loader'

    // include super module
    def modulesSuper = [];
    projInclude(modulesSuper, "super-module")
    manifestModules.put('super', modulesSuper)

    // include modules
    inclModule('core')
    inclModule('minecraft')

    // include library coordinates
    def libs = [];
    {
        rootProject.subprojects {
            project.configurations.lib.resolvedConfiguration.resolvedArtifacts.each { f ->
                libs += f.moduleVersion.id.toString()
            }
        }
    }


    manifest.attributes([
            'Main-Class'             : 'eu.software4you.ulib.loader.launch.Main',
            'Premain-Class'          : 'eu.software4you.ulib.loader.agent.AgentMain',
            'Agent-Class'            : 'eu.software4you.ulib.loader.agent.AgentMain',
            'Can-Redefine-Classes'   : true,
            'Can-Retransform-Classes': true,
            'Libraries'              : libs,
            'Modules'                : manifestModules
    ])
}

extraJavaModuleInfo {
    automaticModule('bungeecord-api-1.17-R0.1-SNAPSHOT.jar', 'bungeecord.api')
}